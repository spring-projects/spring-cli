plugins {
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.graalvm.buildtools.native' version '0.9.19' apply false
	id 'io.spring.antora.generate-antora-yml' version '0.0.1'
	id 'org.antora' version '1.0.0'
	id 'org.jreleaser.jdks' version '1.7.0'
	id 'io.spring.javaformat' version '0.0.41' apply false
}

def useNative = project.hasProperty('springCliNative') && springCliNative.toBoolean()
def useChecks = project.hasProperty('springCliChecks') ? springCliChecks.toBoolean() : true

if (useNative) {
	apply plugin: 'org.graalvm.buildtools.native'
}

if (useChecks) {
	apply plugin: 'io.spring.javaformat'
	apply plugin: 'checkstyle'

	checkstyle {
		toolVersion = '10.12.4'
		configDirectory = file("src/checkstyle")
	}
}

description = 'Spring Cli'
group = 'org.springframework.experimental'
sourceCompatibility = '17'

repositories {
	mavenCentral()
	mavenLocal()
	if (version.contains('-')) {
		maven { url "https://repo.spring.io/milestone" }
	}
	if (version.endsWith('-SNAPSHOT')) {
		maven { url "https://repo.spring.io/snapshot" }
	}
}

ext {
	set('springShellVersion', '3.2.4')
	set('initializrVersion', '0.11.1')
	set('jarchivelibVersion', '1.2.0')
	set('kohsukeVersion', '1.301')
	set('gitlab4jVersion', '4.19.0')
	set('openrewriteVersion', '8.13.4')
	set('antVersion', '1.10.9')
	set('mavenModelVersion', '3.5.4')
	set('tikaVersion', '1.18')
	set('jimfsVersion', '1.2')
	set('diffUtilsVersion', '4.12')
	set('systemStubsVersion', '2.0.1')
	set('handlebarsVersion', '4.1.2')
	set('jsonpathVersion', '2.7.0')
	set('jaxbApiVersion', '2.3.1')
	set('jaxbRuntimeVersion', '2.3.3')
	set('jacksonVersion', '2.15.0')
	set('openaiVersion', '0.12.0')
	set('commonmarkVersion', '0.21.0')
}

configurations.all {
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation "org.springframework.shell:spring-shell-starter-jna"
	implementation 'io.spring.initializr:initializr-generator'
	implementation 'io.projectreactor.addons:reactor-extra'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
	implementation 'org.rauschig:jarchivelib'
	implementation 'org.kohsuke:github-api'
	implementation ('org.gitlab4j:gitlab4j-api') {
		exclude group: 'commons-logging', module: 'commons-logging'
	}
	implementation 'org.openrewrite:rewrite-java-17'
	implementation 'org.openrewrite:rewrite-maven'
	implementation 'org.openrewrite:rewrite-xml'
	implementation 'org.apache.ant:ant'
	implementation 'org.apache.maven:maven-model'
	implementation 'org.apache.tika:tika-core'
	implementation 'com.github.jknack:handlebars'
	implementation 'com.github.jknack:handlebars-humanize'
	implementation 'com.jayway.jsonpath:json-path'
	implementation 'javax.xml.bind:jaxb-api'
	implementation 'org.glassfish.jaxb:jaxb-runtime'
	implementation 'com.theokanning.openai-gpt3-java:service'
	implementation 'org.commonmark:commonmark'

	testImplementation 'org.openrewrite:rewrite-test'
	testImplementation 'uk.org.webcompere:system-stubs-jupiter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.google.jimfs:jimfs'
	testImplementation 'com.squareup.okhttp3:mockwebserver'
	testImplementation 'io.github.java-diff-utils:java-diff-utils'

	if (useChecks) {
		checkstyle("io.spring.javaformat:spring-javaformat-checkstyle:0.0.41")
	}
}

dependencyManagement {
	dependencies {
		dependency "org.springframework.shell:spring-shell-starter-jna:${springShellVersion}"
		dependency "org.rauschig:jarchivelib:${jarchivelibVersion}"
		dependency "org.kohsuke:github-api:${kohsukeVersion}"
		dependency "org.gitlab4j:gitlab4j-api:${gitlab4jVersion}"
		dependency "org.openrewrite:rewrite-test:${openrewriteVersion}"
		dependency "org.openrewrite:rewrite-java-17:${openrewriteVersion}"
		dependency "org.openrewrite:rewrite-xml:${openrewriteVersion}"
		dependency("org.openrewrite:rewrite-maven:${openrewriteVersion}") {
			exclude 'org.slf4j:jcl-over-slf4j'
		}
		dependency "org.apache.ant:ant:${antVersion}"
		dependency "org.apache.maven:maven-model:${mavenModelVersion}"
		dependency "org.apache.tika:tika-core:${tikaVersion}"
		dependency "com.github.jknack:handlebars:${handlebarsVersion}"
		dependency "com.github.jknack:handlebars-humanize:${handlebarsVersion}"
		dependency "com.jayway.jsonpath:json-path:${jsonpathVersion}"
		dependency "com.google.jimfs:jimfs:${jimfsVersion}"
		dependency "io.github.java-diff-utils:java-diff-utils:${diffUtilsVersion}"
		dependency "uk.org.webcompere:system-stubs-jupiter:${systemStubsVersion}"
		dependency "javax.xml.bind:jaxb-api:${jaxbApiVersion}"
		dependency "org.glassfish.jaxb:jaxb-runtime:${jaxbRuntimeVersion}"
		dependency "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
		dependency "com.theokanning.openai-gpt3-java:service:${openaiVersion}"
		dependency "org.commonmark:commonmark:${commonmarkVersion}"
	}
	imports {
		mavenBom "org.springframework.shell:spring-shell-dependencies:${springShellVersion}"
		mavenBom "io.spring.initializr:initializr-bom:${initializrVersion}"
	}
}

test {
	useJUnitPlatform()
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integrationTest/java')
		}
		resources.srcDir file('src/integrationTest/resources')
	}
}

task integrationTest(type: Test) {
	useJUnitPlatform()
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
	integrationTestImplementation.extendsFrom testImplementation
}

rootProject.tasks.named("processIntegrationTestResources") {
	duplicatesStrategy = 'include'
}

task generateGitProperties {
	doLast {
		try {
			def branch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
			def revision = 'git rev-list --max-count 1 --timestamp HEAD'.execute().text.trim()
			def commitHash = revision.split(' ').last()
			def timestamp = revision ? new java.util.Date(java.util.concurrent.TimeUnit.SECONDS.toMillis(revision.split(' ').first() as long)).format("yyyy-MM-dd'T'HH:mm:ssZ") : null
			def commitHashShort = 'git rev-parse --short HEAD'.execute().text.trim()

			File resourcesDir = new File(project.getBuildDir(), 'resources/main')
			File propertiesFile = new File(resourcesDir, 'git.properties')

			if(timestamp) {
				if(!propertiesFile.exists()) {
					resourcesDir.mkdirs()
					propertiesFile.createNewFile()
				}
				propertiesFile.text = """git.branch=${branch}
git.commit.id=${commitHash}
git.commit.id.abbrev=${commitHashShort}
git.commit.time=${timestamp}
"""
			} else {
				project.logger.error('Unable to generate Git properties file.')
			}
		} catch (e) {
			project.logger.error('Unable to generate Git properties file.', e)
		}
	}
}

project.tasks.bootJar.dependsOn('generateGitProperties')

springBoot {
	buildInfo()
}

if (useNative) {
	graalvmNative {
		binaries {
			main {
				imageName = 'spring'
			}
		}
		metadataRepository {
			enabled = true
		}
	}
	// disable aot generated stuff when we have native plugin
	// which enables aot related things
	tasks.named('checkFormatAot') {
		enabled = false
	}

	tasks.named('checkFormatAotTest') {
		enabled = false
	}
}

node {
	version = '16.15.0'
}

antora {
	version = '3.2.0-alpha.2'
	playbook = 'docs/cached-antora-playbook.yml'
	playbookProvider {
		repository = 'spring-projects-experimental/spring-cli'
		branch = 'docs-build'
		path = 'lib/antora/templates/per-branch-antora-playbook.yml'
		checkLocalBranch = true
	}
	options = ['--clean', '--stacktrace']
	dependencies = [
		'@antora/atlas-extension': '1.0.0-alpha.1',
		'@antora/collector-extension': '1.0.0-alpha.3',
		'@asciidoctor/tabs': '1.0.0-beta.3',
		'@opendevise/antora-release-line-extension': '1.0.0',
		'@springio/antora-extensions': '1.8.0',
		'@springio/asciidoctor-extensions': '1.0.0-alpha.9'
	]
}

tasks.named("generateAntoraYml") {
	asciidocAttributes = project.provider( {
		return  ["project-version": project.version ]
	} )
	baseAntoraYmlFile = file('docs/antora.yml')
}

tasks.create("generateAntoraResources") {
	dependsOn 'generateAntoraYml'
}

if (project.hasProperty('cliRelease') && cliRelease.toBoolean()) {
	jdks {
		bellsoft17Osx {
			platform = 'osx-x86_64'
			url = 'https://download.bell-sw.com/java/17.0.9+11/bellsoft-jdk17.0.9+11-macos-amd64.tar.gz'
			checksum = 'sha1/9cce19a8be85dcd3bd61df1e4a62aacccb6599ed'
		}
		bellsoft17OsxArm {
			platform = 'osx-aarch64'
			url = 'https://download.bell-sw.com/java/17.0.9+11/bellsoft-jdk17.0.9+11-macos-aarch64.tar.gz'
			checksum = 'sha1/c2b82658fb2305aaabce6ec5bbc8f15093dee6dd'
		}
		bellsoft17Linux {
			platform = 'linux-x86_64'
			url = 'https://download.bell-sw.com/java/17.0.9+11/bellsoft-jdk17.0.9+11-linux-amd64.tar.gz'
			checksum = 'sha1/af3de4524646058018c7b885b54f7606c5d0a592'
		}
		bellsoft17Windows {
			platform = 'windows-x86_64'
			url = 'https://download.bell-sw.com/java/17.0.9+11/bellsoft-jdk17.0.9+11-windows-amd64.zip'
			checksum = 'sha1/c70350612b58dd0482b707955c0407d3250e8677'
		}
	}
}

task milestoneVersion {
	doLast{
		def postfix = project.getProperty('springCliMilestone')
		if (!(postfix ==~ /(?:M|RC)\d+/)) {
			throw new GradleException('Illegal milestone version')
		}
		def properties = new Properties()
		def file = new File('gradle.properties')
		properties.load(file.newDataInputStream())
		def currentVersion = properties.getProperty('version')
		if (currentVersion.indexOf('-SNAPSHOT') > 0) {
			def nextVersion = currentVersion - '-SNAPSHOT' + '-' + postfix
			properties.setProperty('version', nextVersion)
			properties.store(file.newWriter(), null)
		} else {
			throw new GradleException('Version is not a SNAPSHOT version')
		}
	}
}
